plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
    id 'base'
    id 'idea'
}

subprojects {
    // Do not configure solr-common here
    if (project.name.equals("alfred-telemetry-solr-common")) {
        return;
    }

    apply from: "${project.projectDir}/overload.gradle"

    plugins.withId('java') {
        dependencies {
            implementation project(':alfred-telemetry-solr:alfred-telemetry-solr-common')
        }
    }

    plugins.withId('eu.xenit.docker') {
        dockerFile {
            dockerFile = createDockerFile.getDestFile()
            dockerBuild {
                repository = "hub.xenit.eu/alfred-telemetry/solr-alfred-telemetry-${solrFlavor}"
                automaticTags = false
                tags = ["${version}"]
            }

        }
    }

    plugins.withId('com.github.johnrengelman.shadow') {
        shadowJar {
            dependencies {
                exclude(dependency('org.slf4j:slf4j-api'))
            }
            ext.relocatePackage = { pkg -> relocate(pkg, "eu.xenit.alfred.telemetry.solr.internal.shadow.${pkg}") }
            relocatePackage('com.codahale')
            relocatePackage('com.rabbitmq')
            relocatePackage('io.github.mweirauch')
            relocatePackage('io.micrometer')
            relocatePackage('io.prometheus')
            relocatePackage('org.HdrHistogram')
            relocatePackage('org.LatencyUtils')
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    project.shadow.component(it)
                }
            }
        }
    }
}
