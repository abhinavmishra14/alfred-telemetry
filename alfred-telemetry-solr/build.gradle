plugins {
    id 'eu.xenit.docker' version '5.0.7' apply false
    id 'java'
    id 'idea'
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()

        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public'
        }
    }

    apply plugin: 'java'

    configurations {
        // configuration that holds the classes for graphite metrics, which conflicts with the library provided by solr6
        metricsGraphite
    }

    dependencies {
        implementation("io.micrometer:micrometer-core:1.6.1")
        implementation("io.github.mweirauch:micrometer-jvm-extras:0.1.2")
        implementation "io.micrometer:micrometer-registry-prometheus:1.6.1"
        implementation "io.micrometer:micrometer-registry-graphite:1.6.1"
        metricsGraphite "io.dropwizard.metrics:metrics-graphite:4.1.15"
    }

    if(!(project.name.equals("common"))) {
        apply from: "${project.projectDir}/overload.gradle"
        apply plugin: 'eu.xenit.docker'


        compileJava {
            if ("solr4".equals(solrFlavor)) {
                sourceCompatibility = '1.8'
                targetCompatibility = '1.8'
            } else {
                sourceCompatibility = '11'
                targetCompatibility = '11'
            }
        }

        configurations {
            // configuration that holds classes to include in the jar
            commonClasses
        }

        dependencies {
            commonClasses project(':alfred-telemetry-solr:common')
            configurations.compile.extendsFrom(configurations.commonClasses)

            if ("solr4".equals(solrFlavor)) {
                implementation "org.apache.solr:solr-core:${solrVersion}"
                implementation "org.alfresco:alfresco-solrclient:${alfrescoVersion}"
                implementation "org.alfresco:alfresco-solr4:${alfrescoVersion}:classes@jar"
                implementation "org.apache.tomcat:tomcat-catalina:7.0.92"
            }   else if ("solr6".equals(solrFlavor)) {
                implementation "org.apache.solr:solr-core:${solrVersion}"
                implementation "org.alfresco:alfresco-search:${assVersion}"
            }
        }

        // can this be done better?
        jar {
            from {
               configurations.commonClasses.collect {
                   include 'eu/xenit/**'
                   include 'io/micrometer/**'
                   include 'io/prometheus/**'
                   include 'io/github/mweirauch/**'
                   include 'com/codahale/**'
                   zipTree(it)
               }
            }
        }

        task createDockerFile(type: eu.xenit.gradle.docker.tasks.DockerfileWithCopyTask) {
            dependsOn(jar)
            group 'docker'

            from "${solrBaseImage}"
            if("solr4".equals(solrFlavor)) {
                smartCopy "${rootProject.projectDir}/integration-tests/src/test/resources/${solrFlavor}/solrconfig.xml", "/opt/alfresco/solr4/workspace-SpacesStore/conf/solrconfig.xml"
                smartCopy "${rootProject.projectDir}/integration-tests/src/test/resources/${solrFlavor}/solrconfig.xml", "/opt/alfresco/solr4/archive-SpacesStore/conf/solrconfig.xml"
                smartCopy "${rootProject.projectDir}/integration-tests/src/test/resources/${solrFlavor}/95-init-solr-micrometer-metrics.sh", "/docker-entrypoint.d/"
                smartCopy files(jar).getSingleFile(), "/opt/alfresco/solr4/lib/alfred-telemetry-solr.jar"
            } else if("solr6".equals(solrFlavor)){
                dependsOn(configurations.metricsGraphite)
                // overwrites existing metrics-graphite library with the one used by micrometer
                smartCopy configurations.implementation.fileCollection { it.group == "io.dropwizard.metrics" && it.name == "metrics-graphite"}, "/opt/alfresco-search-services/solr/server/lib/"
                instruction 'RUN rm -f /opt/alfresco-search-services/solr/server/lib/metrics-graphite-3.2.2.jar'

                smartCopy "${rootProject.projectDir}/integration-tests/src/test/resources/${solrFlavor}/95-init-solr-micrometer-metrics.sh", "/docker-entrypoint.d/"
                smartCopy "${rootProject.projectDir}/integration-tests/src/test/resources/${solrFlavor}/solrconfig.xml", "/opt/alfresco-search-services/solrhome/templates/rerank/conf/solrconfig.xml"
                smartCopy files(jar).getSingleFile(), "/opt/alfresco-search-services/solrhome/lib/alfred-telemetry-solr.jar"
            }
        }

        dockerFile {
            dockerFile = createDockerFile.getDestFile()
            dockerBuild {
                repository = "hub.xenit.eu/alfred-telemetry/solr-alfred-telemetry-${solrFlavor}"
                automaticTags = false
                tags = ["${version}"]
            }

        }
    } else {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        dependencies {
            // compile against solr4 libraries
            implementation "org.alfresco:alfresco-solr4:5.2.g:classes@jar"
            implementation "org.alfresco:alfresco-solrclient:5.2.g"
            implementation "org.apache.solr:solr-core:4.10.3"
        }
    }
}
