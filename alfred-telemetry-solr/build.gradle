plugins {
    id 'eu.xenit.docker' version '5.1.1' apply false
    id 'java'
    id 'idea'
}

subprojects {
    // Do not configure solr-common here
    if (project.name.equals("alfred-telemetry-solr-common")) {
        return;
    }

    apply plugin: 'java'
    apply from: '../dependencies.gradle'
    apply from: "${project.projectDir}/overload.gradle"

    configurations {
        // configuration that holds classes to include in the jar
        commonClasses
    }

    dependencies {
        commonClasses project(':alfred-telemetry-solr:alfred-telemetry-solr-common')
        configurations.compile.extendsFrom(configurations.commonClasses)
    }

    // can this be done better?
    jar {
        from {
            configurations.commonClasses.collect {
                include 'eu/xenit/**'
                include 'io/micrometer/**'
                include 'io/prometheus/**'
                include 'io/github/mweirauch/**'
                include 'com/codahale/**'
                zipTree(it)
            }
        }
    }

    plugins.withId('eu.xenit.docker') {
        dockerFile {
            dockerFile = createDockerFile.getDestFile()
            dockerBuild {
                repository = "hub.xenit.eu/alfred-telemetry/solr-alfred-telemetry-${solrFlavor}"
                automaticTags = false
                tags = ["${version}"]
            }

        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact tasks.jar
            }
        }
    }
}
